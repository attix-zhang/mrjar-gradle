plugins {
    id 'java-library'
}

sourceSets {
    java11 {
        java {
            srcDirs 'src/java11/java'
        }
    }
}

dependencies {
    // This is only necessary because in real life, we have dependencies between classes
    // and what you're likely to want to do, is to provide a JDK 11 specific class, which depends on common
    // classes of the main source set. In other words, you want to override some specific classes, but they
    // still have dependencies onto other classes.
    // We want to avoid recompiling all those classes, so we're just saying that the Java 11 specific classes
    // "depend on" the main ones.
    java11Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
}

tasks.named('compileJava') {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.named('compileJava11Java') {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    into('META-INF/versions/11') {
        from sourceSets.java11.output
    }
    manifest.attributes (
        'Multi-Release': 'true', 
        'Main-Class': 'com.mrjar.gradle.App'
    )
}

tasks.register("runOn8", JavaExec, {
    classpath files(jar.archiveFile)
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
})

tasks.register("runOn11", JavaExec, {
    classpath files(jar.archiveFile)
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
})

tasks.register("runOn9", JavaExec, {
    classpath files(jar.archiveFile)
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(9)
    }
})

tasks.register("runOn14", JavaExec, {
    classpath files(jar.archiveFile)
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(14)
    }
})
